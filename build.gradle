/*******************************************************************************
 * Copyright 2016 - 2017 Sparta Systems, Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied.
 *
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

group = 'com.spartasystems.holdmail'
description = 'holdmail'

buildscript {

    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/release' }
        maven { url 'https://plugins.gradle.org/m2' }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE'
        classpath 'gradle.plugin.org.nosphere.apache:creadur-rat-gradle:0.2.1'
        classpath "gradle.plugin.com.netflix.nebula:gradle-ospackage-plugin:3.6.1"
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
        classpath "gradle.plugin.com.github.jk1:gradle-license-report:0.3.5"
        classpath 'net.researchgate:gradle-release:2.4.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/release' }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "jacoco"
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: "org.nosphere.apache.rat"
apply plugin: "nebula.ospackage"
apply plugin: 'com.moowork.node'
apply plugin: "com.github.jk1.dependency-license-report"
apply plugin: 'net.researchgate.release'
apply plugin: 'com.jfrog.bintray'


sourceCompatibility = '1.8'
targetCompatibility = '1.8'


dependencies {

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }

    compile group: 'com.h2database', name: 'h2', version: '1.4.193'
    compile group: 'org.subethamail', name: 'subethasmtp', version: '3.1.7'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.apache.james', name: 'apache-mime4j', version: '0.7.2'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '4.0.3'

    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.5.2'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.6'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.6'
    testCompile group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '2.1.7'
    testCompile group: 'io.rest-assured', name:'rest-assured', version: '3.0.1'
    testCompile group: 'io.rest-assured', name:'spring-mock-mvc', version: '3.0.1'
    testCompile group: 'io.rest-assured', name:'json-schema-validator', version: '3.0.1'
    testCompile group: 'com.github.springtestdbunit', name: 'spring-test-dbunit', version: '1.3.0'
    testCompile(group: 'org.dbunit', name: 'dbunit', version: '2.5.1') {
        exclude(module: 'junit')
    }

    // Our license makes distribution of mysql-connector-java undesirable, uncomment here for local mysql testing
    // runtime group: 'mysql', name: 'mysql-connector-java', version: '6.0.5'
}

bootRun {
    addResources = true
}

springBoot {
    executable = true
    excludeDevtools = true
    layout = 'ZIP' // puts PropertiesLauncher in manifest, needed to support loader.dir
    buildInfo()
}

rat {
    excludes = [
            'node_modules/**',
            '.idea/**',
            'README.md',
            'docker/',
            'src/main/resources/static/js/bundle/holdmail.js',
            'src/main/resources/db/migration/*.sql',
            'src/main/resources/banner.txt',
            'src/test/resources/mails/**',
            'scripts/rpm/**',
            '**/.gradle/**',
            'gradle/**',
            '*gradle*',
            '**/build/**',
            '**/*.json',
            '.travis.yml'
    ]
}

licenseReport {
    outputDir = "$projectDir/build/thirdparty-licenses/thirdparty-licenses"
}

jar.dependsOn generateLicenseReport

jar {

    manifest {
        attributes("Implementation-Version": project.version)
        attributes("Implementation-Title": "HoldMail")
        attributes("Implementation-Vendor": "Sparta Systems Inc.")
    }

    from { ['LICENSE', 'NOTICE', "$projectDir/build/thirdparty-licenses"] }
}

def RPM_INSTALL_DIR = '/opt/holdmail'
def RPM_USER = 'holdmail'
def RPM_GROUP = 'holdmail'

// guide:
// https://github.com/nebula-plugins/gradle-ospackage-plugin/wiki/Usage-Example
ospackage {
    packageName = "${project.name}"
    version = "${project.version}".replace("-SNAPSHOT","")
    vendor = "Sparta Systems Inc."
    release = 1
    arch = NOARCH
    os = LINUX
    type = BINARY

    preInstall file('scripts/rpm/preInstall.sh')
    postInstall file('scripts/rpm/postInstall.sh')
    preUninstall file('scripts/rpm/preUninstall.sh')
    postUninstall file('scripts/rpm/postUninstall.sh')

    into RPM_INSTALL_DIR
    user RPM_USER
    permissionGroup RPM_GROUP

    from(jar.outputs.files) {
        rename { String f -> f.replace("-${project.version}", "") }
        into 'bin'
        fileMode = 0500
    }

    // Spring boot will look for conf files in the same dir as the app JAR file
    // see 'CONF_FOLDER' in http://docs.spring.io/spring-boot/docs/current/reference/html/deployment-install.html
    from("scripts/bin") {
        fileType CONFIG | NOREPLACE
        into "bin"
    }

}

buildRpm {

    user RPM_USER
    permissionGroup RPM_GROUP

    // we can do this because the springBoot plugin above makes the jar executable
    link('/etc/init.d/holdmail', RPM_INSTALL_DIR + '/bin/holdmail.jar')
}


task npmTest(type: NpmTask) {
    args = ['run', 'test']
}


test.dependsOn npmTest
compileJava.dependsOn('npmInstall')
tasks.build.dependsOn('buildRpm')

task srcJAR(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

task docJAR(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives srcJAR
    archives docJAR
}

// gradle release -Prelease.useAutomaticVersion=true -Prelease.releaseVersion=1.0.0 -Prelease.newVersion=1.1.0-SNAPSHOT
release {
    preCommitText = '[RELEASE]'
    preTagCommitMessage = ' - Update to release version: '
    tagCommitMessage = ' - Tagging release version : '
    newVersionCommitMessage = ' Update to new dev version: '
    tagTemplate = '${version}'
}

afterReleaseBuild.dependsOn bintrayUpload

publishing {
    publications {
        HoldmailPublication(MavenPublication) {
            from components.java
            artifact srcJAR
            artifact docJAR
            artifactId 'holdmail'
            groupId project.group
            version project.version
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['HoldmailPublication']

    filesSpec {
        from 'build/distributions'
        into 'rpms'
    }


    pkg {
        repo = 'HoldMail'
        name = 'holdmail'
        userOrg = 'spartasystems'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/spartasystems/holdmail'

        version {
            gpg {
                sign = true
            }
        }
    }

}
