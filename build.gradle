/*******************************************************************************
 * Copyright 2016 - 2017 Sparta Systems, Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied.
 *
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id 'org.springframework.boot' version '1.5.8.RELEASE'
    id "org.nosphere.apache.rat" version "0.3.0"
    id "nebula.ospackage" version "3.6.1"
    id "com.moowork.node" version "1.1.1"
    id "com.github.jk1.dependency-license-report" version "0.3.9"
    id "net.researchgate.release" version "2.5.0"
    id "com.jfrog.bintray" version "1.7.3"
}


group = 'com.spartasystems.holdmail'
description = 'holdmail'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {

    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-devtools'
    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }

    compile 'com.h2database:h2:1.4.196'
    compile 'org.subethamail:subethasmtp:3.1.7'
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'commons-io:commons-io:2.5'	
    compile 'org.apache.james:apache-mime4j:0.8.1'
    compile 'org.flywaydb:flyway-core:4.2.0'
	
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.powermock:powermock-module-junit4:1.7.3'
    testCompile 'org.powermock:powermock-api-mockito:1.7.3'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:2.3.3'
    testCompile 'io.rest-assured:rest-assured:3.0.5'
    testCompile 'io.rest-assured:spring-mock-mvc:3.0.5'
    testCompile 'io.rest-assured:json-schema-validator:3.0.5'
    testCompile 'com.github.springtestdbunit:spring-test-dbunit:1.3.0'
    testCompile('org.dbunit:dbunit:2.5.4') {
        exclude(module: 'junit')
    }

    // Our license makes distribution of mysql-connector-java undesirable, uncomment here for local mysql testing
    // runtime 'mysql:mysql-connector-java:6.0.5'
}

bootRun {
    addResources = true
}

springBoot {
    executable = true
    excludeDevtools = true
    layout = 'ZIP' // puts PropertiesLauncher in manifest, needed to support loader.dir
    buildInfo()
}

rat {
    excludes = [
            'client/node_modules/**',
            'client/dist/**',
            'client/.babelrc',
            'client/.editorconfig',
            'client/.eslintignore',
            'client/.eslintrc.js',
            'client/.postcssrc.js',
            'client/README.md',
            'client/config/',
            'client/static/.gitkeep',
            'client/test/unit/.eslintrc',
            'client/test/unit/coverage/',
            'client/test/e2e/custom-assertions/elementCount.js',
            'client/test/e2e/nightwatch.conf.js',
            'client/test/e2e/runner.js',
            'client/test/e2e/specs/test.js',
            '.idea/**',
            '**/*.iml',
            'README.md',
            'CONTRIBUTING.md',
            '.github/*',
            'docker/',
            'src/main/resources/db/migration/*.sql',
            'src/main/resources/banner.txt',
            'src/main/resources/static/**',
            'src/test/resources/mails/**',
            'scripts/rpm/**',
            '**/.gradle/**',
            'gradle/**',
            '*gradle*',
            '**/build/**',
            '**/*.json',
            '.travis.yml'
    ]
}

licenseReport {
    outputDir = "$projectDir/build/thirdparty-licenses/thirdparty-licenses"
}

jar.dependsOn generateLicenseReport

jar {

    manifest {
        attributes("Implementation-Version": project.version)
        attributes("Implementation-Title": "HoldMail")
        attributes("Implementation-Vendor": "Sparta Systems Inc.")
    }

    from { ['LICENSE', 'NOTICE', "$projectDir/build/thirdparty-licenses"] }
}

def RPM_INSTALL_DIR = '/opt/holdmail'
def RPM_USER = 'holdmail'
def RPM_GROUP = 'holdmail'

// guide:
// https://github.com/nebula-plugins/gradle-ospackage-plugin/wiki/Usage-Example
ospackage {
    packageName = "${project.name}"
    version = "${project.version}".replace("-SNAPSHOT", "")
    vendor = "Sparta Systems Inc."
    release = 1
    arch = NOARCH
    os = LINUX
    type = BINARY

    preInstall file('scripts/rpm/preInstall.sh')
    postInstall file('scripts/rpm/postInstall.sh')
    preUninstall file('scripts/rpm/preUninstall.sh')
    postUninstall file('scripts/rpm/postUninstall.sh')

    into RPM_INSTALL_DIR
    user RPM_USER
    permissionGroup RPM_GROUP

    from(jar.outputs.files) {
        rename { String f -> f.replace("-${project.version}", "") }
        into 'bin'
        fileMode = 0500
    }

    // Spring boot will look for conf files in the same dir as the app JAR file
    // see 'CONF_FOLDER' in http://docs.spring.io/spring-boot/docs/current/reference/html/deployment-install.html
    from("scripts/bin") {
        fileType CONFIG | NOREPLACE
        into "bin"
    }

}

buildRpm {

    user RPM_USER
    permissionGroup RPM_GROUP

    // we can do this because the springBoot plugin above makes the jar executable
    link('/etc/init.d/holdmail', RPM_INSTALL_DIR + '/bin/holdmail.jar')
}

task runNpmInstall(type: NpmTask) {
  workingDir = file("${project.projectDir}/client")

  args = ['install']
}

task compileUI(type: NpmTask) {
  dependsOn 'runNpmInstall'

  workingDir = file("${project.projectDir}/client")

  args = ['run', 'ci']
}

task buildUI(type: Copy) {
  dependsOn 'compileUI'

  from "${project.projectDir}/client/dist"
  into "${project.projectDir}/src/main/resources/static"
}

test {
    testLogging {
        // default test failure logging doesn't show assertion failure detail (unless gradle -i is used)
        exceptionFormat = 'full'
    }
}

compileJava.dependsOn('buildUI')
tasks.build.dependsOn('buildRpm')

task srcJAR(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

task docJAR(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives srcJAR
    archives docJAR
}

// gradle release -Prelease.useAutomaticVersion=true -Prelease.releaseVersion=1.0.0 -Prelease.newVersion=1.1.0-SNAPSHOT
release {
    preCommitText = '[RELEASE]'
    preTagCommitMessage = ' - Update to release version: '
    tagCommitMessage = ' - Tagging release version : '
    newVersionCommitMessage = ' Update to new dev version: '
    tagTemplate = '${version}'
}

afterReleaseBuild.dependsOn bintrayUpload

publishing {
    publications {
        HoldmailPublication(MavenPublication) {
            from components.java
            artifact srcJAR
            artifact docJAR
            artifactId 'holdmail'
            groupId project.group
            version project.version
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['HoldmailPublication']

    filesSpec {
        from 'build/distributions'
        into 'rpms'
    }


    pkg {
        repo = 'HoldMail'
        name = 'holdmail'
        userOrg = 'spartasystems'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/spartasystems/holdmail'

        version {
            gpg {
                sign = true
            }
        }
    }

}
